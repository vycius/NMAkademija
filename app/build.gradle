plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'findbugs'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.nmakademija.nmaakademija"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 115
        versionName "1.1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        xmlReport true
    }
}

ext {
    firebaseVersion = '11.2.0'
    supportVersion = '26.0.1'
    leakCanaryVersion = '1.5.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:animated-vector-drawable:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"

    testCompile 'junit:junit:4.12'

    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    compile 'com.facebook.android:facebook-android-sdk:4.23.0'

    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.google.firebase:firebase-perf:$firebaseVersion"

    compile 'com.github.bumptech.glide:glide:3.8.0'

    compile 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.2'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}

// https://github.com/ben-manes/gradle-versions-plugin
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    excludeFilter = new File("${project.rootDir}/app/findbugs_filter.xml")
    classes = files("build/intermediates/classes/")
    classes += files("${project.rootDir}/app/build/intermediates/classes/")
    source 'src/main'
    source "${project.rootDir}/app/src/main"
    include '**/*.java'
    reports {
        xml {
            enabled = true
            withMessages = true
        }
        html {
            enabled = false
        }

    }
    classpath = files()
}

apply plugin: 'com.google.gms.google-services'
